VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

'フィールド番号を表す列挙型の定義
Enum eFieldsSheet1
    eId = 1
    eName
    eGender
    eBirthday
    eActive
End Enum

'コレクションの定義
Public Persons As Collection
Public MaxId As Long 'IDの最大値

'**
'* テーブル上にデータをPersonsコレクションとして格納する
'*
Public Sub LoadData()
    Set Persons = New Collection
    
    With ListObjects(1)
        Dim myRow As ListRow
        For Each myRow In .ListRows
        
            Dim p As Person: Set p = New Person
            p.Initialize myRow.Range
            Persons.Add p, CStr(p.Id) 'キーはString型
            
        Next myRow
        
        MaxId = .ListRows.Count
    End With
End Sub

''**
''* Personsコレクションのデータをテーブルに適用する(遅い版)
''*
'Public Sub ApplyData()
'
'    ListObjects(1).ShowAutoFilter = False
'    Me.Unprotect "hr1857299"
'
'    With ListObjects(1)
'        If .ListRows.Count > 0 Then .DataBodyRange.EntireRow.Delete
'
'        Dim p As Person
'        For Each p In Persons
'            Dim values As Variant
'            values = Array(p.Id, p.Name, p.Gender, p.Birthday, p.Active)
'            .ListRows.Add.Range = values
'        Next p
'
'        MaxId = .ListRows.Count
'    End With
'
'    Me.Protect "hr1857299", AllowFiltering:=True
'    ListObjects(1).ShowAutoFilter = True
'
'End Sub
'**
'* Personsコレクションのデータをテーブルに適用する(高速版)
'* 2次元配列によるテーブルの更新
'*
Public Sub ApplyData()

    ListObjects(1).ShowAutoFilter = False
    Me.Unprotect "hr1857299"
    
    With ListObjects(1)
        If .ListRows.Count > 0 Then .DataBodyRange.EntireRow.Delete
        
        MaxId = Persons.Count
        Dim values() As Variant: ReDim values(1 To MaxId, 1 To 5)
        
        Dim p As Person
        For Each p In Persons
            values(p.Id, eId) = p.Id
            values(p.Id, eName) = p.Name
            values(p.Id, eGender) = p.Gender
            values(p.Id, eBirthday) = p.Birthday
            values(p.Id, eActive) = p.Active
        Next p
        
        Dim baseRange As Range: Set baseRange = .Range(1, 1).Offset(1)
        baseRange.Resize(MaxId, 5).Value = values
        
    End With
    
    Me.Protect "hr1857299", AllowFiltering:=True
    ListObjects(1).ShowAutoFilter = True
    
End Sub

'**
'* PersonsコレクションのPersonオブジェクトを更新する
'*
'* @param p {Person} 更新するPersonオブジェクト
'*
Public Sub UpdatePerson(p As Person)
    
    With Persons(p.Id)
        .Id = p.Id
        .Name = p.Name
        .Gender = p.Gender
        .Birthday = p.Birthday
        .Active = p.Active
    End With
    
    Call ApplyData
End Sub

'**
'* PersonコレクションにPersonオブジェクトを追加する
'*
'* @param p {Person} 追加するPersonオブジェクト
'*
Public Sub AddPerson(p As Person)
    Persons.Add p, CStr(p.Id)
    Call ApplyData
End Sub
